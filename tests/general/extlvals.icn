#  Test use of external values with dynamic loading, using demo cfunc.

link cfunc

record complex(r, i)

procedure main()
   local e, l, v1, v2, v3, v4

   # test simple creation, type(), copy(), ===, ~===
   v1 := extv0()
   v2 := extv0()
   v3 := v1
   v4 := copy(v1)
   write("v1: ", type(v1), ": ", image(v1))
   write(image(v1 === v2)  | "v1 === v2 failed correctly")
   write(image(v1 ~=== v2) | "v1 ~=== v2 failed correctly")
   write(image(v1 === v3)  | "v1 === v3 failed incorrectly")
   write(image(v1 ~=== v3) | "v1 ~=== v3 failed correctly")
   write(image(v1 === v4)  | "v1 === v4 failed incorrectly")
   write(image(v1 ~=== v4) | "v1 ~=== v4 failed correctly")
   write()

   # test sorting
   l := [
      extv1(6.6),
      extv0(),
      extv1(3.3),
      extv0(),
      extv1(1.1),
      extv0(),
      extv1(4.4),
      extv0(),
      extv1(2.2),
      extv0(),
      extv1(5.5),
      complex(1, -1),
      ]
   every e := !sort(l) do {
      writes(left(type(e) || ":", 10), image(e))
      writes(" => ", image(e ~===:= copy(e)))
      write()
   }
end

procedure gen(p, a)
   write(image(p), "(", image(a), ") = ", p(a) | "[failed]")
   return
end
