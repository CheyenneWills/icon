############################################################################
#
#	File:     tgrmerge.icn
#
#	Subject:  Program to merge line chain files
#
#	Author:   Gregg M. Townsend
#
#	Date:     January 20, 1996
#
############################################################################
#
#   usage:  tgrmerge file.lch ...
#
#   Tgrquant merges multiple line chain files to produce a single
#   output file.
#
############################################################################

procedure main(args)
   local f, fname, line, lat, lon
   local minlat, maxlat, minlon, maxlon

   if *args = 0 then
      stop("usage: ", &progname, " file.lch ...")
   minlat := minlon := 9999999
   maxlat := maxlon := 0

   every fname := !args do {
      f := open(fname) | stop("can't open ", fname)
      line := read(f) | stop("empty file: ", fname)
      line ? {
         move(8)
	 lat := move(7)
	 lon := move(7)
         minlon >:= lat
         minlat >:= lon
	 }
      line := read(f) | stop("truncated file: ", fname)
      line ? {
         move(8)
	 lat := move(7)
	 lon := move(7)
         maxlon <:= lat
         maxlat <:= lon
	 }
      close(f)
      }

   write("        ", right(minlon, 7), right(minlat, 7))
   write("        ", right(maxlon, 7), right(maxlat, 7))
   every fname := !args do {
      f := open(fname) | stop("can't open ", fname)
      read(f)
      read(f)
      while write(read(f))
      close(f)
      }
end
